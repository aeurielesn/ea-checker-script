//===== eAthena Script =======================================
//= Checker Game
//===== By: ==================================================
//= Xyphen
//= pakpil
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena SVN 10580+
//===== Description: =========================================
//= Amusing Checker Game
//===== Additional Comments: =================================
//= These ones are the game control functions. These will
//= control game procces, like pieces movements,
//= Checker transfer, etc.
//============================================================ 

function	script	F_AbsolutePos	{
	return (getarg(0)*3)+9;
	}

function	script	F_RelativePos	{
	return (getarg(0)-9)/3;
	}

function	script	F_BoardPos	{
	return getarg(0)+(8*getarg(1));
	}

// return....
// 0: Empty
// 1: Poring
// 2: Angeling
function	script	F_PieceType	{
	if(getarg(0)<25) return 1;
	else if(getarg(0)>24) return 2;
	else return 0;
	}

function	script	F_Player	{
	if(!getarg(0)) return 0;
	if(callfunc("F_PieceType",getarg(0))==1) return (getarg(0)/13)+1;
	else return ((getarg(0)-24)/13)+1;
	}

// getarg(0)    coordenada x
// getarg(1)    coordenada y
// getarg(2)    0: Value, 1: # Player, 2: Poring or Angeling?
function	script	F_BoardChecker	{
	if((getarg(0) < 0) || (getarg(0) > 7)) return -1;
	if((getarg(1) < 0) || (getarg(1) > 7)) return -1;
	set .@result, $@cboard[callfunc("F_BoardPos",getarg(0),getarg(1))];
	if(getarg(2)==0) return .@result;
	else if(getarg(2)==1) return callfunc("F_Player",.@result);
	else if(getarg(2)==2) return callfunc("F_PieceType",.@result);
	}

function	script	F_GhostLoader	{
	if(!callfunc("F_BoardChecker",getarg(0),getarg(1),1)){
		set $@nGhost, $@nGhost + 1;
		movenpc "Poporing#" + $@nGhost,callfunc("F_AbsolutePos",getarg(0)),callfunc("F_AbsolutePos",getarg(1));
		enablenpc "Poporing#" + $@nGhost;
		}
	return;
	}

//	getarg(0)	Final X-relative
//	getarg(1)	Final Y-relative
//	getarg(2)	Current X-relative
//	getarg(3)	Current X-relative
function	script	F_TakePiece	{
	if(getarg(0) > getarg(2)) set .@todelx, getarg(0)-1;
	else set .@todelx, getarg(0)+1;

	if(getarg(1) > getarg(3)) set .@todely, getarg(1)-1;
	else set .@todely, getarg(1)+1;
                        
	set .@todelp, callfunc("F_BoardChecker", .@todelx, .@todely, 0);
	if(callfunc("F_PieceType", .@todelp)==1) disablenpc "Poring#" + .@todelp;
	else disablenpc "Angeling#" + (.@todelp % 24);

	set $@cboard[callfunc("F_BoardPos", .@todelx, .@todely)], 0;
	set $@rPieces[callfunc("F_Player",.@todelp)], $@rPieces[callfunc("F_Player",.@todelp)] - 1;
	//comprobacion de final de juego

	return;
	}

function	script	F_ChangeType	{
	if(getarg(0)>24) return -1;
	disablenpc "Poring#" + getarg(0);
	movenpc "Angeling#" + getarg(0), callfunc("F_AbsolutePos",$@xPiece[getarg(0)]),callfunc("F_AbsolutePos",$@yPiece[getarg(0)]);
	enablenpc "Angeling#" + getarg(0);
	set $@cboard[callfunc("F_BoardPos",$@xPiece[getarg(0)],$@yPiece[getarg(0)])], 24 + getarg(0);
	return;
	}



function	script	F_GhostUnloader	{
	if(select("Mover","No mover")==1){
		getmapxy( .@map$, .@lastx, .@lasty, 1 ,"Poporing#"+getarg(0));
		for( set .@i, 1; .@i <= $@nGhost; set .@i, .@i + 1 ) disablenpc "Poporing#" + .@i;
		set $@nGhost, 0;

		if(callfunc("F_PieceType",$@MovePiece)==2) set .@piece, $@MovePiece%24;
		else set .@piece, $@MovePiece;

		set .@pos, callfunc("F_BoardPos",$@xPiece[.@piece],$@yPiece[.@piece]);
		set $@cboard[.@pos], 0;

		if($@TakePiece) callfunc "F_TakePiece",callfunc("F_RelativePos", .@lastx),callfunc("F_RelativePos", .@lasty),$@xPiece[.@piece],$@yPiece[.@piece];

		set $@xPiece[.@piece], callfunc("F_RelativePos", .@lastx);
		set $@yPiece[.@piece], callfunc("F_RelativePos", .@lasty);

		set .@pos, callfunc("F_BoardPos",$@xPiece[.@piece],$@yPiece[.@piece]);
		set $@cboard[.@pos], $@MovePiece;

		if(callfunc("F_PieceType",$@MovePiece)==1) movenpc "Poring#" + .@piece, .@lastx, .@lasty;
		else movenpc "Angeling#" + .@piece, .@lastx, .@lasty;

		if($@TakePiece){
			if(callfunc("F_PieceType",$@MovePiece)==1){
				if(callfunc("F_Player",$@MovePiece)==1) set .@posible, callfunc("F_Possibles",$@xPiece[.@piece],$@yPiece[.@piece],1,callfunc("F_Player",$@MovePiece));
				else set .@posible, callfunc("F_Possibles",$@xPiece[.@piece],$@yPiece[.@piece],-1,callfunc("F_Player",$@MovePiece));
				
				if(.@posible&10){ 
					if(callfunc("F_Player",$@MovePiece)==1) callfunc "F_MoveChecker",$@MovePiece,1,1;
					else callfunc "F_MoveChecker",.@piece,-1,1;
				} else { 
					set $@TakePiece, 0;
					}
			} else {
				set .@posible, callfunc("F_Possibles2",$@xPiece[.@piece],$@yPiece[.@piece],callfunc("F_Player",$@MovePiece));

				if(.@posible&170){ 
					callfunc "F_MoveChecker2",.@piece,1;
				} else { 
					set $@TakePiece, 0;
					}
				}
			}

		if(!($@TakePiece)) {
			set $@MovePiece, 0;
			set $@CheckTurn, 3 - $@CheckTurn;
			}
		close;
		}
	return;
	}

// getarg(2)    +1: North, -1:South
function	script	F_Possibles	{
	set .@posible,0;
	set .@player, getarg(3);
	set .@izq, callfunc("F_BoardChecker",getarg(0)-1,getarg(1)+getarg(2),1);
	set .@der, callfunc("F_BoardChecker",getarg(0)+1,getarg(1)+getarg(2),1);
 
	if(!(.@izq)) set .@posible, .@posible|1;

	if((.@izq > 0) && (.@izq != .@player))
		if(!callfunc("F_BoardChecker",getarg(0)-2,getarg(1)+2*getarg(2),1))
			set .@posible, .@posible|2;

	if(!(.@der)) set .@posible, .@posible|4;

	if((.@der > 0) && (.@der != .@player))
		if(!callfunc("F_BoardChecker",getarg(0)+2,getarg(1)+2*getarg(2),1))
			set .@posible, .@posible|8;

	return .@posible;
	}

// .@posible
// .........
// .2.....8.
// ..*...*..
// ...1.4...
// ....P....
// .........
//
// getarg(1)    +1: North, -1:South
// getarg(2)	0: menu, 1: no menu
function	script	F_MoveChecker	{
	set .@x,$@xPiece[getarg(0)];
	set .@y,$@yPiece[getarg(0)];
	set .@posible, callfunc("F_Possibles",.@x,.@y,getarg(1),callfunc("F_Player",getarg(0)));

	if(($@MovePiece && !(getarg(2)) ) || !(.@posible)) return;

	if(!getarg(2)) set .@choose,select("Mover","No mover");
	else set .@choose, 1;

	if(.@choose==1){
		set $@MovePiece,getarg(0);
		if((.@posible&1) && !(.@posible&10)) callfunc "F_GhostLoader",.@x-1,.@y+getarg(1);
		if((.@posible&4) && !(.@posible&10)) callfunc "F_GhostLoader",.@x+1,.@y+getarg(1);
		if(.@posible&2) { callfunc "F_GhostLoader",.@x-2,.@y+(2*getarg(1)); set $@TakePiece,1; }
		if(.@posible&8) { callfunc "F_GhostLoader",.@x+2,.@y+(2*getarg(1)); set $@TakePiece,1; }
		if(!getarg(2)) close;
		}
		
	return;
	}

function	script	F_Possibles2	{
	set .@posible, 0;

	for(set .@i, 1; callfunc("F_BoardChecker",getarg(0) - .@i,getarg(1) + .@i,1)==0 ;set .@i, .@i + 1)
		set .@posible, .@posible|1;

	set .@posible, .@posible|(callfunc("F_Possibles",getarg(0) - .@i + 1, getarg(1) + .@i - 1, 1,getarg(2))&2);

	for(set .@i, 1; callfunc("F_BoardChecker",getarg(0) + .@i,getarg(1) + .@i,1)==0 ;set .@i, .@i + 1)
		set .@posible, .@posible|4;

	set .@posible, .@posible|(callfunc("F_Possibles",getarg(0) + .@i - 1, getarg(1) + .@i - 1, 1,getarg(2))&8);

	for(set .@i, 1; callfunc("F_BoardChecker",getarg(0) - .@i,getarg(1) - .@i,1)==0 ;set .@i, .@i + 1)
        	set .@posible, .@posible|16;
	
	set .@posible, .@posible|((callfunc("F_Possibles",getarg(0) - .@i + 1,getarg(1) - .@i + 1, -1,getarg(2))&2) << 4);

	for(set .@i, 1; callfunc("F_BoardChecker",getarg(0) + .@i,getarg(1) - .@i,1)==0 ;set .@i, .@i + 1)
		set .@posible, .@posible|64;

	set .@posible, .@posible|((callfunc("F_Possibles",getarg(0) + .@i - 1,getarg(1) - .@i + 1, -1,getarg(2))&8) << 4);

	return .@posible;
	}

// ........................
// ......2.....8...........
// .......*...*............
// ........1.4.............
// .........A..............
// .......16.64............
// .......*...*............
// .....32.....128.........
// ........................
function	script	F_MoveChecker2	{
	set .@x, $@xPiece[getarg(0)];
	set .@y, $@yPiece[getarg(0)];
	set .@posible, callfunc("F_Possibles2",.@x,.@y,callfunc("F_Player",getarg(0)));

	if(( $@MovePiece && !(getarg(1)) ) || ( .@posible == 0 )) return;

	if(!getarg(1)) set .@choose, select("Mover","No mover");
	else set .@choose,1;
   
	if(.@choose){
		set $@MovePiece, 24 + getarg(0);

		for(set .@i, 1; callfunc("F_BoardChecker",.@x - .@i,.@y + .@i,1)==0 ;set .@i, .@i + 1)
			if((.@posible&1) && !(.@posible&170)) callfunc "F_GhostLoader",.@x - .@i,.@y + .@i;
		
		if(.@posible&2) { callfunc "F_GhostLoader",.@x - .@i - 1,.@y + .@i + 1; set $@TakePiece,1; }

		for(set .@i, 1; callfunc("F_BoardChecker",.@x + .@i,.@y + .@i,1)==0 ;set .@i, .@i + 1)
			if((.@posible&4) && !(.@posible&170)) callfunc "F_GhostLoader",.@x + .@i,.@y + .@i;

		if(.@posible&8) { callfunc "F_GhostLoader",.@x + .@i + 1,.@y + .@i + 1; set $@TakePiece,1; }

		for(set .@i, 1; callfunc("F_BoardChecker",.@x - .@i,.@y - .@i,1)==0 ;set .@i, .@i + 1)
			if((.@posible&16) && !(.@posible&170)) callfunc "F_GhostLoader",.@x - .@i,.@y - .@i;

		if(.@posible&32) { callfunc "F_GhostLoader",.@x - .@i - 1,.@y - .@i - 1; set $@TakePiece,1; }

		for(set .@i, 1; callfunc("F_BoardChecker",.@x + .@i,.@y - .@i,1)==0 ;set .@i, .@i + 1)
			if((.@posible&64) && !(.@posible&170)) callfunc "F_GhostLoader",.@x + .@i,.@y - .@i;

		if(.@posible&128) { callfunc "F_GhostLoader",.@x + .@i + 1,.@y - .@i - 1; set $@TakePiece,1; }

		if(!getarg(1)) close;
		}
	return;
	}

// getarg(0)	char id
// getarg(1)	piece to move
// getarg(2)	
function	script	F_CheckTurn	{
	if(getarg(0)==$@WPlayer && getarg(1)==1){
		if($@CheckTurn==1) return 1;
		else dispbottom "Es el turno de las negras";
	} else if(getarg(0)==$@BPlayer && getarg(1)==2){
		if($@CheckTurn==2) return 1;
		else dispbottom "Es el turno de las blancas";
	} else {
		dispbottom "No tienes permiso para mover está ficha.";
		}
	return 0;
}