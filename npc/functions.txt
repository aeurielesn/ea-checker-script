//===== eAthena Script =======================================
//= Checker Game
//===== By: ==================================================
//= Xyphen
//= pakpil
//===== Current Version: =====================================
//= 5.0
//===== Compatible With: =====================================
//= eAthena SVN 14410+
//===== Description: =========================================
//= Amusing Checker Game
//===== Additional Comments: =================================
//= Game control functions.
//============================================================ 

//**
//* Process if exists obligatory moves.
//*/
//TODO: optimize
function	script	F_ObligatoryMoves	{
   set .@obligatory, 0;
	if($@CheckTurn==1) {
		for( set .@i, 1; .@i <= 12; set .@i, .@i + 1 ) {
			set .@posible, 0;
			set .@piece, $@cboard[callfunc("F_BoardPos", $@xPiece[.@i], $@yPiece[.@i])];
			set .@type, callfunc("F_PieceType", .@piece); 
			if(.@type == 1) {
				set .@posible, callfunc("F_Possibles", $@xPiece[.@i], $@yPiece[.@i], 1, 1);
				if(.@posible&10) {
					set $@canMove[.@i], 0;
					set .@obligatory, .@obligatory|1;
				} else set $@canMove[.@i], 1;
            } else if(.@type==2) {
				set .@posible, callfunc("F_Possibles2", $@xPiece[.@i], $@yPiece[.@i],1);
				if(.@posible&170) {
					set $@canMove[.@i], 0;
					set .@obligatory, .@obligatory|1;
				} else set $@canMove[.@i], 1;
            }
		}
		if(.@obligatory) {
			for( set .@i, 1; .@i <= 12; set .@i, .@i + 1 ) {
				set $@canMove[.@i], 1 - $@canMove[.@i];
			}
		}
		for( set .@i, 1; .@i <= 12; set .@i, .@i + 1 ) {
			set .@piece, $@cboard[callfunc("F_BoardPos", $@xPiece[.@i], $@yPiece[.@i])];
			set .@type, callfunc("F_PieceType", .@piece);
			if(.@type && $@canMove[.@i]) {
				// FIXME: kings
				if(.@type==1) donpcevent "Poring#" + .@i + "::OnEmote";
			}
		}
	} else if($@CheckTurn==2) {
		for( set .@i, 13; .@i <= 24; set .@i, .@i + 1 ) {
			set .@posible, 0;
			set .@piece, $@cboard[callfunc("F_BoardPos", $@xPiece[.@i], $@yPiece[.@i])];
			set .@type, callfunc("F_PieceType", .@piece); 
			if(.@type == 1) {
				set .@posible, callfunc("F_Possibles", $@xPiece[.@i], $@yPiece[.@i], -1, 2);
				if(.@posible&10) {
					set $@canMove[.@i], 0;
					set .@obligatory, .@obligatory|1;
               } else set $@canMove[.@i], 1;
            } else if(.@type==2) {
				set .@posible, callfunc("F_Possibles2",$@xPiece[.@i],$@yPiece[.@i],2);
				if(.@posible&170) {
					set $@canMove[.@i], 0;
					set .@obligatory, .@obligatory|1;
				} else set $@canMove[.@i], 1;
			}
		}
		if(.@obligatory) {
			for( set .@i, 13; .@i <= 24; set .@i, .@i + 1 ) {
				set $@canMove[.@i], 1 - $@canMove[.@i];
			}
		}
		for( set .@i, 13; .@i <= 24; set .@i, .@i + 1 ) {
			set .@piece, $@cboard[callfunc("F_BoardPos", $@xPiece[.@i], $@yPiece[.@i])];
			set .@type, callfunc("F_PieceType", .@piece);
			if(.@type && $@canMove[.@i]) {
				// FIXME: kings
				if(.@type==1) donpcevent "Poring#" + .@i + "::OnEmote";
			}
		}		
	}
	return .@obligatory;
}

///**
//* Checks the end of the game.
//* return - 1 for end of game, 0 otherwise
//*/
function	script	F_EndOfGame	{
	set .@endgame, 1;
	if($@CheckTurn==1){
		for( set .@i, 1; (.@i <= 12) && (.@endgame == 1); set .@i, .@i + 1 ){
			set .@posible, 0;
			set .@piece, $@cboard[callfunc("F_BoardPos", $@xPiece[.@i], $@yPiece[.@i])];
			set .@type, callfunc("F_PieceType",.@piece); 
			if(.@type==1) set .@posible, callfunc("F_Possibles",$@xPiece[.@i],$@yPiece[.@i],1,1);
			else if(.@type==2) set .@posible, callfunc("F_Possibles2",$@xPiece[.@i],$@yPiece[.@i],1);
			if(.@posible) set .@endgame, 0;
			}
	} else if($@CheckTurn==2){
		for( set .@i, 13; (.@i <= 24) && (.@endgame == 1); set .@i, .@i + 1 ){
			set .@posible, 0;
			set .@piece, $@cboard[callfunc("F_BoardPos", $@xPiece[.@i], $@yPiece[.@i])];
			set .@type, callfunc("F_PieceType",.@piece); 
			if(.@type==1) set .@posible, callfunc("F_Possibles",$@xPiece[.@i],$@yPiece[.@i],-1,2);
			else if(.@type==2) set .@posible, callfunc("F_Possibles2",$@xPiece[.@i],$@yPiece[.@i],2);
			if(.@posible) set .@endgame, 0;
			}
		}
	return .@endgame;
	}

///**
//* Retrieve the absolute position given the relative position.
//* args - relative position
//* return - absolute position
//*/
function	script	F_AbsolutePos	{
	return (getarg(0)*3)+9;
	}

///**
//* Retrieve the relative position given the absolute position.
//* args - absolute position
// return - relative position
//*/
function	script	F_RelativePos	{
	return (getarg(0)-9)/3;
	}

///**
//* Retrieve the board vectorial position given the
//* matriz position of a specific cell.
//* args - matrixial coordinates x and y
//* return - a number between 0...63 that represents the
//*	position in the vectorial form
//*/
function	script	F_BoardPos	{
	return getarg(0)+(8*getarg(1));
	}

///**
//* Retrieve the type of a specific piece.
//* args - value of the piece
//* return - 1 for a man, 2 for a king, 0 otherwise.
//*/
function	script	F_PieceType	{
	if((getarg(0)>=1) && (getarg(0)<=24)) return 1;
	else if((getarg(0)>=25) && (getarg(0)<=48)) return 2;
	else return 0;
	}

///**
//* Retrieve the player that owns a specific piece.
//* args - value of the piece
//* return - 1 or 2 for the player, 0 for empty.
//*/
function	script	F_Player	{
	if(!getarg(0)) return 0;
	if(callfunc("F_PieceType",getarg(0))==1) return (getarg(0)/13)+1;
	else return ((getarg(0)-24)/13)+1;
	}

///**
//* Retrieve the value of the piece, the player or the type of the piece
//* in a specific relative position of the board.
//* args - x-coordinate, y-coordinate and result-type
//* return - value of the cell for result-type 0,
//* 	player owner for result-type 1,
//*	piece's type for result-type 2.
//*/
function	script	F_BoardChecker	{
	if((getarg(0) < 0) || (getarg(0) > 7)) return -1;
	if((getarg(1) < 0) || (getarg(1) > 7)) return -1;
	set .@result, $@cboard[callfunc("F_BoardPos",getarg(0),getarg(1))];
	if(getarg(2)==0) return .@result;
	else if(getarg(2)==1) return callfunc("F_Player",.@result);
	else if(getarg(2)==2) return callfunc("F_PieceType",.@result);
	}

///**
//* Loads the ghost pieces for the piece's movement.
//*/
function	script	F_GhostLoader	{
	if(!callfunc("F_BoardChecker",getarg(0),getarg(1),1)){
		set $@nGhost, $@nGhost + 1;
		movenpc "Poporing#" + $@nGhost,callfunc("F_AbsolutePos",getarg(0)),callfunc("F_AbsolutePos",getarg(1));
		enablenpc "Poporing#" + $@nGhost;
		}
	return;
	}

///**
//* Take a piece of the oponent given the move of the attacker
//* args - final x-relative, final y-relative, 
//*	current x-relative, current y-relative
//*/
function	script	F_TakePiece	{
	if(getarg(0) > getarg(2)) set .@todelx, getarg(0)-1;
	else set .@todelx, getarg(0)+1;

	if(getarg(1) > getarg(3)) set .@todely, getarg(1)-1;
	else set .@todely, getarg(1)+1;
                        
	set .@todelp, callfunc("F_BoardChecker", .@todelx, .@todely, 0);
	if(callfunc("F_PieceType", .@todelp)==1) disablenpc "Poring#" + .@todelp;
	else disablenpc "Angeling#" + ((.@todelp%25)+1);

	set $@cboard[callfunc("F_BoardPos", .@todelx, .@todely)], 0;
	set $@rPieces[callfunc("F_Player",.@todelp)], $@rPieces[callfunc("F_Player",.@todelp)] - 1;

	// TODO: Comprobacion de final de juego
	// El jugador toma todas las fichas del oponente
	if($@rPieces[callfunc("F_Player",.@todelp)]==0)
		dispbottom "Victoria del jugador actual.";
	
	return;
	}

///**
//* Change a normal piece to a checker piece.
//* args - piece id
//*/
function	script	F_ChangeType	{
	if((getarg(0) < 0) || (getarg(0)>24)) return -1;
	disablenpc "Poring#" + getarg(0);
	movenpc "Angeling#" + getarg(0), callfunc("F_AbsolutePos",$@xPiece[getarg(0)]),callfunc("F_AbsolutePos",$@yPiece[getarg(0)]);
	enablenpc "Angeling#" + getarg(0);
	set $@cboard[callfunc("F_BoardPos",$@xPiece[getarg(0)],$@yPiece[getarg(0)])], 24 + getarg(0);
	return;
	}

///**
//* Checks for the position selected to move a specific piece.
//* args - piece id
//*/
function	script	F_GhostUnloader	{
	if(select("Mover","No mover")==1) {
		getmapxy( .@map$, .@lastx, .@lasty, 1 ,"Poporing#"+getarg(0));
		for( set .@i, 1; .@i <= $@nGhost; set .@i, .@i + 1 ) disablenpc "Poporing#" + .@i;
		set $@nGhost, 0;

		set .@type, callfunc("F_PieceType", $@MovePiece);

		if(.@type==2) set .@piece, ($@MovePiece%25)+1;
		else set .@piece, $@MovePiece;

		set .@pos, callfunc("F_BoardPos",$@xPiece[.@piece],$@yPiece[.@piece]);
		set $@cboard[.@pos], 0;

		if($@TakePiece) callfunc "F_TakePiece",callfunc("F_RelativePos", .@lastx),callfunc("F_RelativePos", .@lasty),$@xPiece[.@piece],$@yPiece[.@piece];

		set $@xPiece[.@piece], callfunc("F_RelativePos", .@lastx);
		set $@yPiece[.@piece], callfunc("F_RelativePos", .@lasty);

		set .@pos, callfunc("F_BoardPos",$@xPiece[.@piece],$@yPiece[.@piece]);
		set $@cboard[.@pos], $@MovePiece;

		if(.@type==1) movenpc "Poring#" + .@piece, .@lastx, .@lasty;
		else movenpc "Angeling#" + .@piece, .@lastx, .@lasty;

		// TODO: Comprobacion de coronar una ficha
		if(.@type==1){
			if((callfunc("F_Player",$@MovePiece)==2) && ($@yPiece[.@piece]==0)) callfunc("F_ChangeType",$@MovePiece);
			if((callfunc("F_Player",$@MovePiece)==1) && ($@yPiece[.@piece]==7)) callfunc("F_ChangeType",$@MovePiece);
			}

		if($@TakePiece) {
			if(.@type==1) {
				// FIXME: callfunc("F_Player",$@MovePiece) can be omitible
				if(callfunc("F_Player",$@MovePiece)==1) set .@posible, callfunc("F_Possibles",$@xPiece[.@piece],$@yPiece[.@piece],1,callfunc("F_Player",$@MovePiece));
				else set .@posible, callfunc("F_Possibles",$@xPiece[.@piece],$@yPiece[.@piece],-1,callfunc("F_Player",$@MovePiece));
				
				if(.@posible&10) { 
					if(callfunc("F_Player",$@MovePiece)==1) {
						// FIXME: emote for kings too
						if(.@type==1) donpcevent "Poring#" + .@piece + "::OnEmote";
						callfunc "F_MoveChecker",$@MovePiece, 1, 1;
						for( set .@i, 1; .@i <= 12; set .@i, .@i + 1 ) {
							set $@canMove[.@i], 0;
						}
					} else {
						callfunc "F_MoveChecker",.@piece, -1, 1;
						for( set .@i, 13; .@i <= 24; set .@i, .@i + 1 ) {
							set $@canMove[.@i], 0;
						}
					}
					set $@canMove[.@piece], 1;
				} else { 
					set $@TakePiece, 0;
				}
			} else {
				set .@posible, callfunc("F_Possibles2",$@xPiece[.@piece],$@yPiece[.@piece],callfunc("F_Player",$@MovePiece));

				if(.@posible&170) { 
					callfunc "F_MoveChecker2",.@piece,1;
					if(callfunc("F_Player",$@MovePiece)==1) {
						for( set .@i, 1; .@i <= 12; set .@i, .@i + 1 ) {
							set $@canMove[.@i], 0;
						}
					} else {
						for( set .@i, 13; .@i <= 24; set .@i, .@i + 1 ) {
							set $@canMove[.@i], 0;
						}
					}
					set $@canMove[.@piece], 1;
				} else { 
					set $@TakePiece, 0;
				}
			}
		}

		if(!($@TakePiece)) {
			set $@MovePiece, 0;
			set $@CheckTurn, 3 - $@CheckTurn;

			//TODO: end of game
			if(callfunc("F_EndOfGame")==1)
				dispbottom "End of Game.";
			
			//TODO: comprobacion antes del inicio del turno
			callfunc "F_ObligatoryMoves";

		}
		close;
	}
	else close;

	return;
}

///**
// Retrieve a bitwise variable with the possibles of a specific piece given
//* the relative coordinates.
//* args - x-relative, y-relative, direction (+1: north, -1:south), player moving
//* return - an integer
//*/
// .@posible
// .........
// .2.....8.
// ..*...*..
// ...1.4...
// ....P....
// .........
function	script	F_Possibles	{
	set .@posible,0;
	set .@player,getarg(3);
	set .@izq, callfunc("F_BoardChecker",getarg(0)-1,getarg(1)+getarg(2),1);
	set .@der, callfunc("F_BoardChecker",getarg(0)+1,getarg(1)+getarg(2),1);
 
	if(!(.@izq)) set .@posible, .@posible|1;

	if((.@izq > 0) && (.@izq != .@player))
		if(!callfunc("F_BoardChecker",getarg(0)-2,getarg(1)+2*getarg(2),1))
			set .@posible, .@posible|2;

	if(!(.@der)) set .@posible, .@posible|4;

	if((.@der > 0) && (.@der != .@player))
		if(!callfunc("F_BoardChecker",getarg(0)+2,getarg(1)+2*getarg(2),1))
			set .@posible, .@posible|8;

	return .@posible;
	}

///**
//* Function to move a normal piece
//* args - piece id, display menu (yes:0, no:1)
//*/
function	script	F_MoveChecker	{
	set .@piece, getarg(0);
	set .@player, callfunc("F_Player", .@piece);
	set .@x, $@xPiece[.@piece];
	set .@y, $@yPiece[.@piece];
	set .@facing, callfunc("F_PieceFacing", .@piece);
	
	set .@posible, callfunc("F_Possibles", .@x, .@y, .@facing, .@player);

	if(($@MovePiece && !(getarg(2))) || !(.@posible)) return;
	if(!(getarg(2)) && !($@canMove[.@piece])) return;

	if(!getarg(2)) set .@choose, select("Mover", "No mover");
	else set .@choose, 1;

	if(.@choose==1){
		set $@MovePiece, .@piece;
		if((.@posible&1) && !(.@posible&10)) callfunc "F_GhostLoader", .@x-1, .@y+.@facing;
		if((.@posible&4) && !(.@posible&10)) callfunc "F_GhostLoader", .@x+1, .@y+.@facing;
		if(.@posible&2) { callfunc "F_GhostLoader", .@x-2, .@y+(2*.@facing); set $@TakePiece, 1; }
		if(.@posible&8) { callfunc "F_GhostLoader", .@x+2, .@y+(2*.@facing); set $@TakePiece, 1; }
		if(!getarg(2)) close;
		}
	else close;
		
	return;
}

///**
//* Retrieve a bitwise variable with the possibles of a specific piece checker given
//* the relative coordinates.
//* args - x-relative, y-relative, player moving
// return - an integer
//*/
// ........................
// ......2.....8...........
// .......*...*............
// ........1.4.............
// .........A..............
// .......16.64............
// .......*...*............
// .....32.....128.........
// ........................
function	script	F_Possibles2	{
	set .@posible, 0;

	for(set .@i, 1; callfunc("F_BoardChecker",getarg(0) - .@i,getarg(1) + .@i,1)==0 ;set .@i, .@i + 1)
		set .@posible, .@posible|1;

	set .@posible, .@posible|(callfunc("F_Possibles",getarg(0) - .@i + 1, getarg(1) + .@i - 1, 1,getarg(2))&2);

	for(set .@i, 1; callfunc("F_BoardChecker",getarg(0) + .@i,getarg(1) + .@i,1)==0 ;set .@i, .@i + 1)
		set .@posible, .@posible|4;

	set .@posible, .@posible|(callfunc("F_Possibles",getarg(0) + .@i - 1, getarg(1) + .@i - 1, 1,getarg(2))&8);

	for(set .@i, 1; callfunc("F_BoardChecker",getarg(0) - .@i,getarg(1) - .@i,1)==0 ;set .@i, .@i + 1)
        	set .@posible, .@posible|16;
	
	set .@posible, .@posible|((callfunc("F_Possibles",getarg(0) - .@i + 1,getarg(1) - .@i + 1, -1,getarg(2))&2) << 4);

	for(set .@i, 1; callfunc("F_BoardChecker",getarg(0) + .@i,getarg(1) - .@i,1)==0 ;set .@i, .@i + 1)
		set .@posible, .@posible|64;

	set .@posible, .@posible|((callfunc("F_Possibles",getarg(0) + .@i - 1,getarg(1) - .@i + 1, -1,getarg(2))&8) << 4);

	return .@posible;
	}

///**
//* Function to move a checker piece
//* args - piece id, display menu (yes:0, no:1)
//*/
function	script	F_MoveChecker2	{
	set .@x, $@xPiece[getarg(0)];
	set .@y, $@yPiece[getarg(0)];
	set .@posible, callfunc("F_Possibles2",.@x,.@y,callfunc("F_Player",getarg(0)));

	if(( $@MovePiece && !(getarg(1)) ) || ( .@posible == 0 )) return;
	if(!(getarg(1)) && !($@canMove[getarg(0)])) return;

	if(!getarg(1)) set .@choose, select("Mover","No mover");
	else set .@choose,1;
   
	if(.@choose){
		set $@MovePiece, 24 + getarg(0);

		for(set .@i, 1; callfunc("F_BoardChecker",.@x - .@i,.@y + .@i,1)==0 ;set .@i, .@i + 1)
			if((.@posible&1) && !(.@posible&170)) callfunc "F_GhostLoader",.@x - .@i,.@y + .@i;
		
		if(.@posible&2) { callfunc "F_GhostLoader",.@x - .@i - 1,.@y + .@i + 1; set $@TakePiece,1; }

		for(set .@i, 1; callfunc("F_BoardChecker",.@x + .@i,.@y + .@i,1)==0 ;set .@i, .@i + 1)
			if((.@posible&4) && !(.@posible&170)) callfunc "F_GhostLoader",.@x + .@i,.@y + .@i;

		if(.@posible&8) { callfunc "F_GhostLoader",.@x + .@i + 1,.@y + .@i + 1; set $@TakePiece,1; }

		for(set .@i, 1; callfunc("F_BoardChecker",.@x - .@i,.@y - .@i,1)==0 ;set .@i, .@i + 1)
			if((.@posible&16) && !(.@posible&170)) callfunc "F_GhostLoader",.@x - .@i,.@y - .@i;

		if(.@posible&32) { callfunc "F_GhostLoader",.@x - .@i - 1,.@y - .@i - 1; set $@TakePiece,1; }

		for(set .@i, 1; callfunc("F_BoardChecker",.@x + .@i,.@y - .@i,1)==0 ;set .@i, .@i + 1)
			if((.@posible&64) && !(.@posible&170)) callfunc "F_GhostLoader",.@x + .@i,.@y - .@i;

		if(.@posible&128) { callfunc "F_GhostLoader",.@x + .@i + 1,.@y - .@i - 1; set $@TakePiece,1; }

		if(!getarg(1)) close;
		}
	else close;

	return;
	}

///**
//* Checks if the given player is the current player to move.
//* args - char id, piece to move
//* return - yes or no.
//*/
function	script	F_CheckTurn	{
	set .@player, callfunc("F_Player", getarg(1));
	if((getarg(0) == $@WPlayer) && (.@player == 1)){
		if($@CheckTurn == 1) return 1;
		else dispbottom "Es el turno de las negras";
	} else if((getarg(0)==$@BPlayer) && (.@player == 2)){
		if($@CheckTurn == 2) return 1;
		else dispbottom "Es el turno de las blancas";
	} else {
		dispbottom "No tienes permiso para mover esta ficha.";
		}
	return 0;
}

function	script	F_ch_ResetGame	{
	set $@WPlayer, 0;
	set $@BPlayer, 0;
	set $@Opponent$, "";
	set $@CheckReg, 0;
	set $@CheckGame, 0;
	set $@CheckTurn, 0;
	for(set .@i,1; .@i < 25; set .@i, .@i + 1){
		disablenpc "Poring#" + .@i;
		disablenpc "Angeling#" + .@i;
	}
	for(set .@i,1; .@i < 15; set .@i, .@i + 1)
		disablenpc "Poporing#" + .@i;
	return;
}

///**
//* Starts a new game.
//*/
function	script	F_StartGame	{
		setarray $@xPiece[1],0,2,4,6,1,3,5,7,0,2,4,6,7,5,3,1,6,4,2,0,7,5,3,1;
		setarray $@yPiece[1],0,0,0,0,1,1,1,1,2,2,2,2,7,7,7,7,6,6,6,6,5,5,5,5;

		for(set .@i, 0; .@i < 64; set .@i, .@i + 1)
			set $@cboard[.@i], 0;

		for(set .@i, 1; .@i < 25; set .@i, .@i + 1) {
			movenpc "Poring#" + .@i, callfunc("F_AbsolutePos", $@xPiece[.@i]), callfunc("F_AbsolutePos", $@yPiece[.@i]);
			enablenpc "Poring#" + .@i;
		}

		for(set .@i, 1; .@i < 15; set .@i, .@i + 1)
			disablenpc "Poporing#" + .@i;

		for(set .@i, 1; .@i < 25; set .@i, .@i + 1)
			disablenpc "Angeling#" + .@i;

		for(set .@i, 1; .@i < 25; set .@i, .@i + 1)
			set $@cboard[callfunc("F_BoardPos", $@xPiece[.@i], $@yPiece[.@i])], .@i;

		for(set .@i, 1; .@i <= 12; set .@i, .@i + 1)
			set $@canMove[.@i], 1;

		for(set .@i, 13; .@i <= 24; set .@i, .@i + 1)
			set $@canMove[.@i], 0;

		setarray $@rPieces[1],12,12;
		set $@CheckTurn, 1;
		set $@nGhost, 0;
		set $@MovePiece, 0;
		set $@TakePiece, 0;	
		return;
}

///**
//* Return the facing of a man.
//* args - man
//* return - north:+1 or south:-1.
//*/
function	script	F_PieceFacing	{
	set .@piece, getarg(0);
	set .@type, callfunc("F_PieceType", .@piece);
	if(.@type == 1) {
		set .@player, callfunc("F_Player", .@piece);
		if(.@player == 1) return 1;
		else if(.@player == 2) return -1;
	}
	return 0;
}